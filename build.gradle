plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.3'
    id 'io.freefair.lombok' version "8.0.1"
}

group = 'com.grigoryev'
version = '1.0-SNAPSHOT'

sourceCompatibility = targetCompatibility = '17'

repositories {
    mavenLocal()
    mavenCentral()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.22.3"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.56.0'
        }
        javapgv {
            artifact = 'build.buf.protoc-gen-validate:protoc-gen-validate:0.6.13'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            javapgv {
                option "lang=java"
            }
        }
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.grigoryev.grpc.greeting.server.GreetingServer'
        )
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

dependencies {
    runtimeOnly 'io.grpc:grpc-netty-shaded:1.56.0'

    implementation 'io.grpc:grpc-protobuf:1.56.0'
    implementation 'io.grpc:grpc-stub:1.56.0'
    implementation 'build.buf.protoc-gen-validate:pgv-java-stub:1.0.2'
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'ch.qos.logback:logback-core:1.4.8'
    implementation 'ch.qos.logback:logback-classic:1.4.8'
    implementation 'org.mongodb:mongodb-driver-sync:4.10.1'

    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
}
